/*
 * Copyright 2020 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://nexus.jmix.io/repository/public'
        }
    }

    dependencies {
        classpath('io.jmix.gradle:jmix-gradle-plugin:1.0.0-M4')
    }
}

plugins {
    id 'java'
}

apply plugin: 'io.jmix'
apply plugin: 'org.springframework.boot'

jmix {
    bomVersion = '1.0.0'
}

group = 'io.jmix.sampler'

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.jmix.io/repository/public'
    }
    maven {
        url 'https://vaadin.com/nexus/content/repositories/vaadin-addons'
    }
    maven {
        url 'https://nexus.jmix.io/repository/premium'
        credentials {
            username(rootProject.hasProperty('premiumRepoUser') ? rootProject['premiumRepoUser'] : System.getenv('CUBA_PREMIUM_USER'))
            password(rootProject.hasProperty('premiumRepoPass') ? rootProject['premiumRepoPass'] : System.getenv('CUBA_PREMIUM_PASSWORD'))
        }
    }
}

dependencies {
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'
    implementation 'io.jmix.ui:jmix-ui-starter'
    implementation 'io.jmix.ui:jmix-ui-data-starter'
    implementation "io.jmix.ui:jmix-charts-starter"
    implementation "io.jmix.ui:jmix-pivot-table-starter"
    implementation "io.jmix.maps:jmix-maps-starter"
    implementation "io.jmix.maps:jmix-maps-ui-starter"
    implementation 'io.jmix.sessions:jmix-sessions-starter'
    implementation 'io.jmix.translations:jmix-translations-ru'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.commons:commons-dbcp2'
    implementation 'org.vaadin.addons:googleanalyticstracker:3.0.0'

    runtimeOnly 'org.hsqldb:hsqldb'

    themes 'io.jmix.ui:jmix-ui-themes'

    widgets "io.jmix.ui:jmix-charts-widgets"
    widgets "io.jmix.ui:jmix-pivot-table-widgets"
    widgets "io.jmix.maps:jmix-maps-ui-widgets"
    widgets "io.jmix.ui:jmix-ui-widgets"
}

processResources {
    with copySpec {
        from sourceSets.main.allJava
    }
}

compileThemes {
    if (findProject(':ui-themes')) {
        dependsOn project(':ui-themes').tasks.getByName('jar')
    }
}

compileWidgets {
    generate "io.jmix.sampler.widgets.CustomWidgetSet"
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    if (rootProject.hasProperty('jmixDockerTag')) {
        imageName = rootProject['jmixDockerTag']
    } else {
        imageName = 'docker.haulmont.com/platform/infrastructure/demo-cuba-apps-docker/jmix-demo-sampler:testing'
    }
}
